<!doctype html>
<html lang="en">
  <head>
    <meta charset="utf-8">
    <meta name="viewport" content="width=device-width, initial-scale=1, shrink-to-fit=no">
    <link rel="stylesheet" href="static/styles/main.css">
    <title>SPH Three.JS</title>
  </head>
  <body>
    <script type="x-shader/x-vertex" id="physicsVertexShader">
      attribute vec2 reference;
      attribute vec3 pos_offset;
      
      uniform sampler2D texturePosition;
      uniform sampler2D textureAcceleration;
      uniform float scale;
      #include <fog_pars_vertex>

      varying vec4 vColor;
      varying vec3 modelPos;
      varying vec3 Normal;
      varying vec3 Ka;
      varying vec3 Kd;

      void main(){
          
          //vec4 tmpPos = scale * texture2D(texturePosition,reference);
          vec4 tmpPos = texture2D(texturePosition,reference);
          
          float rho = texture2D(textureAcceleration,reference).a;

          float tmpX = scale * tmpPos.x;
          float tmpY = scale * tmpPos.y;
          float tmpZ = scale * tmpPos.z;

          Ka = vec3(1.0,rho,0.0);
          Kd = vec3(1.0,rho,0.0);
          
          //gl_Position=projectionMatrix * modelViewMatrix * vec4(tmpX,tmpY,tmpZ,1.0);
          
          //gl_PointSize= 3.0;
          
          vec3 transformed = vec3(tmpX,tmpY,tmpZ);
          vec4 mvPosition = vec4( position + transformed, 1.0 );

          modelPos = vec3(modelViewMatrix * mvPosition);
          Normal = normalize(normalMatrix * normal);

          //mvPosition = instanceMatrix * mvPosition; 

          mvPosition = modelViewMatrix * mvPosition;
          gl_Position = projectionMatrix * mvPosition;
          
          #include <fog_vertex>

      }
    </script>
    <script type="x-shader/x-fragment" id="pointFragmentShader">
      varying vec3 Normal;
      varying vec3 modelPos;

      //uniform vec3 Ka;
      //uniform vec3 Kd;

      varying vec3 Ka;
      varying vec3 Kd;

      uniform vec3 Ks;

      uniform vec4 LightPosition;
      uniform vec3 LightIntensity;
      uniform float Shininess;

      #include <fog_pars_fragment>
      varying vec4 vColor;

      vec3 phong() {
        vec3 n = normalize(Normal);
        vec3 s = normalize(vec3(LightPosition) - modelPos);
        vec3 v = normalize(vec3(-modelPos));
        vec3 r = reflect(-s, n);

        vec3 ambient = Ka;
        vec3 diffuse = Kd * max(dot(s, n), 0.0);
        vec3 specular = Ks * pow(max(dot(r, v), 0.0), Shininess);

        return LightIntensity * (ambient + diffuse + specular);
      }


      void main(){
        //gl_FragColor=vColor;
        gl_FragColor = vec4(phong(), 1.0);
        #include <fog_fragment>
      }
    </script>


    <script type="x-shader/x-fragment" id="fragmentShaderAcceleration">
      uniform float equationOfStateConst;
      uniform float polytropicIndex;
      uniform float smoothingLength;
      uniform float particleMass;
      uniform float lambda;
      uniform float viscosity;

      const float PI = 3.141592653589793;

      const float width = resolution.x;
			const float height = resolution.y;

      vec3 evaluateKernelDerivative(float xDist, float yDist, float zDist){
        float r = sqrt(xDist*xDist + yDist*yDist + zDist*zDist);

        float n = -2.0 * exp(-(r * r) / (smoothingLength * smoothingLength));
        n /= pow(smoothingLength,5.0);
        n /= pow(PI, (3.0/2.0) );

        return vec3(n * xDist, n * yDist, n * zDist);
      }

      float evaluatePressure(float rho){
        return equationOfStateConst * pow(rho, 1.0 + (1.0 / polytropicIndex) );
      }

      void main(){
        vec2 uv = gl_FragCoord.xy / resolution.xy;
        vec4 tmpPos = texture2D( texturePosition, uv);
        vec4 tmpVel = texture2D( textureVelocity, uv);
        vec4 tmpRho = texture2D( textureDensity, uv);

        float rho = tmpRho.x;
        float pressure = evaluatePressure(rho);

        float selfXPos = tmpPos.x;
        float selfYPos = tmpPos.y;
        float selfZPos = tmpPos.z;

        vec4 otherPosition;
        vec3 gradient;
        float otherPressure;

        float ax=0.0;
        float ay=0.0;
        float az=0.0;

        float gradientZTest = 0.0;

        float rhoMax = rho;
        float rhoMin = rho;

        for (float y = 0.0; y < height; y++){
          for (float x = 0.0; x < width; x++){
            vec2 ref = vec2( x + 0.5, y + 0.5 ) / resolution.xy;

            otherPosition = texture2D(texturePosition, ref);
            float otherRho = texture2D(textureDensity, ref).x;
            float otherPressure = evaluatePressure(otherRho);

            if (otherRho > rhoMax){
              rhoMax = otherRho;
            }

            if (otherRho < rhoMin){
              rhoMin = otherRho;
            }

            float xDist = selfXPos - otherPosition.x;
            float yDist = selfYPos - otherPosition.y;
            float zDist = selfZPos - otherPosition.z;

            gradient = evaluateKernelDerivative(xDist,yDist,zDist);

            float pressureAcc =  (pressure/(rho*rho)) + (otherPressure/(otherRho*otherRho));

            ax += particleMass * pressureAcc * gradient.x;
            ay += particleMass * pressureAcc * gradient.y;
            az += particleMass * pressureAcc * gradient.z;

            gradientZTest += gradient.z;
          }
        }

        ax = -ax - lambda * selfXPos - viscosity * tmpVel.x;
        ay = -ay - lambda * selfYPos - viscosity * tmpVel.y;
        az = -az - lambda * selfZPos - viscosity * tmpVel.z;

        float colorRho = min(15.0, rho);

        //float colorRho = rho;
        rhoMax = min(15.0, rhoMax);

        colorRho = (colorRho - rhoMin) / (rhoMax - rhoMin);


        gl_FragColor = vec4(ax,ay,az,colorRho);

      }
    </script>

    <script type="x-shader/x-fragment" id="fragmentShaderVelocity">  
      uniform float deltaT;

      void main(){
        vec2 uv = gl_FragCoord.xy / resolution.xy;
        vec4 tmpAcc = texture2D( textureAcceleration, uv );
        vec4 tmpVel = texture2D( textureVelocity, uv);

        float xVel = tmpVel.x + tmpAcc.x * 0.5 * deltaT;
        float yVel = tmpVel.y + tmpAcc.y * 0.5 * deltaT;
        float zVel = tmpVel.z + tmpAcc.z * 0.5 * deltaT;

        gl_FragColor = vec4(xVel,yVel,zVel,1.0);
      }
    </script>

    <script type="x-shader/x-fragment" id="fragmentShaderPosition">  
      uniform float deltaT;
      //uniform float smoothingLength;
      //uniform float particleMass;

      //const float PI = 3.141592653589793;

      /*float evaluateKernel(float xDist, float yDist, float zDist){
        float r = sqrt(xDist*xDist + yDist*yDist + zDist*zDist);

        float w = (1.0 / (smoothingLength * sqrt(PI)));
        w = w * w * w * exp( -(r*r) / (smoothingLength*smoothingLength));

        return w;
      }*/

      void main(){
        vec2 uv = gl_FragCoord.xy / resolution.xy;
        vec4 tmpVel = texture2D( textureVelocity, uv);
        vec4 tmpPos = texture2D( texturePosition, uv);

        float selfXPos = tmpPos.x + tmpVel.x * deltaT;
        float selfYPos = tmpPos.y + tmpVel.y * deltaT;
        float selfZPos = tmpPos.z + tmpVel.z * deltaT;

        gl_FragColor = vec4(selfXPos,selfYPos,selfZPos,0.0);
      }
    </script>

    <script type="x-shader/x-fragment" id="fragmentShaderDensity">
      uniform float smoothingLength;
      uniform float particleMass;

      const float PI = 3.141592653589793;

      const float width = resolution.x;
			const float height = resolution.y;

      float evaluateKernel(float xDist, float yDist, float zDist){
        float r = sqrt(xDist*xDist + yDist*yDist + zDist*zDist);

        float w = (1.0 / (smoothingLength * sqrt(PI)));
        w = w * w * w * exp( -(r*r) / (smoothingLength*smoothingLength));

        return w;
      }

      void main(){
        vec2 uv = gl_FragCoord.xy / resolution.xy;
        vec4 selfPos = texture2D( texturePosition, uv);

        vec3 otherPosition;

        float rho = 0.0;

        for (float y = 0.0; y < height; y++){
          for (float x = 0.0; x < width; x++){
            vec2 ref = vec2( x + 0.5, y + 0.5 ) / resolution.xy;

            otherPosition = texture2D(texturePosition, ref).xyz;

            float xDist = selfPos.x - otherPosition.x;
            float yDist = selfPos.y - otherPosition.y;
            float zDist = selfPos.z - otherPosition.z;

            rho += particleMass * evaluateKernel(xDist, yDist, zDist);
          }
        }

        gl_FragColor = vec4(rho,0.0,0.0,0.0);
      }

    </script>

    <!-- Information modal -->
    <a id="open-modal-btn" class="float">
      <i class="icon-float material-icons">menu</i>
    </a>
    <div id="info-modal" class="modal">
      <div class="modal-content">
        <span class="close-modal-btn">&times;</span>
        <h3>Smoothed particle hydrodynamics example with Three.js</h3>
        <p>Three.js/GPU implementation of the <a href="https://philip-mocz.medium.com/create-your-own-smoothed-particle-hydrodynamics-simulation-with-python-76e1cec505f1">tutorial</a> by Philip Mocz. 
          GLSL GPU-based particle computation, rendered in 3D as InstancedMesh with Three.js. 4096 samples. GPUComputationRenderer use 
          demonstrates individual texture computation in sequence.</p>
        <p>Built and deployed rapidly with <a href="https://github.com/aakatev/three-js-webpack">three-js-webpack</a></p>
        <p><a href="https://github.com/myociss/threejs-star-sph">Code</a></p>
      </div>
    </div>
    <!-- End information modal -->

    <script src="public/bundle.js"></script>
  </body>
</html>
